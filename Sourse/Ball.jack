class Ball{
    field int ballX, ballY, ballDX, ballDY;
    field int leftScore, rightScore;
    field boolean flag;
    field Paddles paddles;
    field int leftPaddleY, rightPaddleY;

    constructor Ball new(Paddles paddle){
        let paddles = paddle;
        let leftScore = 0;
        let rightScore = 0;
        return this;
    }

    //перезапуск мяча
    method void resetBall() {
        let ballX = 80;
        let ballY = 50;
        let ballDX = -1;
        let ballDY = -1;
        return;
    }

    method int GetBallX(){
        return ballX;
    }

    method int GetBallY(){
        return ballY;
    }

    method int GetLeftScore(){
        return leftScore;
    }

    method int GetRightScore(){
        return rightScore;
    }

    // обработка движения и отскоков мяча
    method void moveBall() {
        let ballX = ballX + ballDX;
        let ballY = ballY + ballDY;
        let flag = true;
        let leftPaddleY = paddles.GetLeftPaddleY();
        let rightPaddleY = paddles.GetRightPaddleY();

        // отскок от верхней и нижней границы поля
        if ((ballY < 1) | (ballY > 99)) {
            let ballDY = ballDY * (-1);
        }

        // гол в правые ворота
        if (ballX > 157) {
            let leftScore = leftScore + 1;
            do resetBall();
        }

        // гол в левые ворота
        if (ballX < 2)
        {
            let rightScore = rightScore + 1;
            do resetBall();
        }

        // отскок шара от торца правой подвижной стенки
        if ((ballX > 151) & (ballX < 155) & (ballY > (rightPaddleY - 1)) & (ballY < (rightPaddleY + 21))){
            let ballDY = ballDY * (-1);
            let flag = false;
        }

        // отскок шара от торца левой подвижной стенки
        if ((ballX > 4) & (ballX < 8) & (ballY > (leftPaddleY - 1)) & (ballY < (leftPaddleY + 21))){
            let ballDY = ballDY * (-1);
            let flag = false;
        }

        // отскок шара от левой подвижной стенки
        if ((ballX < 9) & (ballX > 6) & (ballY > (leftPaddleY - 1)) & (ballY < (leftPaddleY + 21))) {
            // проверка не дает залететь шару в стенку через торец
            if (flag = true){
            let ballDX = ballDX * -1;
            }
        }

        // отскок шара от правой подвижной стенки
        if ((ballX > 149) & (ballX < 152) & (ballY > (rightPaddleY - 1)) & (ballY < (rightPaddleY + 21))) {
            // проверка не дает залететь шару в стенку через торец
            if (flag = true){
            let ballDX = ballDX * -1;
            }
        }
        
        // отскок шара от статичных боковых границ
        if(((ballX < 3) | (ballX > 156)) & (((ballY > 79) & (ballY < 101)) | ((ballY > 0) & (ballY < 21))))
        {
            let ballDX = ballDX * -1;
        }
        return;
    }
}